buildscript {
	repositories {
		mavenCentral()
		maven { url 'http://public-maven.classmethod.info/release' }
		maven { url "http://repo.spring.io/release" }
	}
	dependencies {
		classpath 'org.hibernate.build.gradle:version-injection-plugin:1.0.0'
		classpath 'jp.classmethod.aws:gradle-aws-plugin:0.11'
		classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.1.8.RELEASE'
	}
}
plugins {
	id "com.github.hierynomus.license" version "0.11.0"
}

defaultTasks 'clean', 'build'


// ======== Generic ========
group = 'jp.classmethod.aws'
version = '0.9'
ext {
	artifactId = 'brian'
	defaultEncoding = 'UTF-8'
}


// ======== Java ========
apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'eclipse'
sourceCompatibility = targetCompatibility = 1.8
tasks.withType(AbstractCompile) each {
	it.options.encoding = ext.defaultEncoding
}

compileJava {
	options.compilerArgs << '-Xlint:deprecation'
}

test {
	exclude '**/*$*', '**/*Tests.class', '**/examples/**'
	reports {
		html.enabled = true
		junitXml.outputPerTestCase = false
	}
}


// ======== dependency management ========
ext {
	// test dependencies
	junitVersion = '4.11'
	hamcrestVersion = '1.3'
	mockitoCoreVersion = '1.9.5'

	// Java spec
	servletApiVersion = '3.1.0'
	
	// loggers
	slf4jVersion = '1.7.7'
	logbackVersion = '1.1.2'
	
	// others
	springVersion = '4.1.1.RELEASE'
	springBootVersion = '1.1.8.RELEASE'
	mysqlVersion = '5.1.22'
	guavaVersion = '18.0'
	quartzVersion = '2.2.0'
	gsonVersion = '2.2.2'
	baseunitsVersion = '2.11'
	awsJavaSdkVersion = '1.9.2'
}

configurations {
	providedRuntime
}
dependencies {
	// Logging
	compile "org.slf4j:slf4j-api:$slf4jVersion"
	compile "org.slf4j:jcl-over-slf4j:$slf4jVersion"
	compile "org.slf4j:log4j-over-slf4j:$slf4jVersion"
	compile "ch.qos.logback:logback-classic:$logbackVersion"
	compile ('org.lazyluke:log4jdbc-remix:0.2.7') {
		exclude group: 'org.slf4j', module: 'slf4j-log4j12'
	}
	
	// Spring
	compile "org.springframework:spring-context-support:$springVersion"
	compile "org.springframework:spring-tx:$springVersion"
	compile "org.springframework:spring-jdbc:$springVersion"
	compile "org.springframework:spring-webmvc:$springVersion"
	compile "org.springframework.boot:spring-boot-starter-web:$springBootVersion"
	
	// Other
	compile "mysql:mysql-connector-java:$mysqlVersion"
	compile "com.google.guava:guava:$guavaVersion"
	compile "commons-dbcp:commons-dbcp:1.4"
	compile "com.google.code.gson:gson:$gsonVersion"
	compile "com.amazonaws:aws-java-sdk-sns:$awsJavaSdkVersion"
	compile "org.quartz-scheduler:quartz:$quartzVersion"
	compile "jp.xet:baseunits:$baseunitsVersion"
	
	// Servlet API
	providedCompile "javax.servlet:javax.servlet-api:$servletApiVersion"
	providedRuntime "org.springframework.boot:spring-boot-starter-tomcat:1.1.8.RELEASE"
	
	// Testing
	testCompile "junit:junit:$junitVersion"
	testCompile "org.mockito:mockito-core:$mockitoCoreVersion"
	testCompile "org.springframework:spring-test:$springVersion"
}

repositories {
	mavenCentral()
	maven {
		url "http://repo.spring.io/release"
	}
	maven {
		// for log4jdbc-remix
		url "https://oss.sonatype.org/content/repositories/releases"
	}
	maven {
		// for baseunits
		url "http://maven.xet.jp/release"
	}
}


// ======== version injection ========
apply plugin: 'version-injection'

versionInjection {
	into('jp.classmethod.aws.brian.Version', 'getVersionString')
}

// ======== license headers ========
apply plugin: 'license'

license {
	ext.year = Calendar.getInstance().get(Calendar.YEAR)
	header rootProject.file('copyright/HEADER')
	strictCheck true
	mapping {
		java = 'SLASHSTAR_STYLE'
	}
	exclude '**/*.json'
}


// ======== create source and javadoc bundles ========
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar
	archives javadocJar
}


// ======== local boot ========
sourceSets.main.output.classesDir = 'src/main/webapp/WEB-INF/classes'
sourceSets.main.output.resourcesDir = 'src/main/webapp/WEB-INF/classes'

clean {
	delete << 'src/main/webapp/WEB-INF/classes'
}

apply plugin: 'spring-boot'

bootRun {
	systemProperty('JDBC_CONNECTION_STRING', project.JDBC_CONNECTION_STRING)
	systemProperty('DB_USERNAME', project.DB_USERNAME)
	systemProperty('DB_PASSWORD', project.DB_PASSWORD)
	systemProperty('BRIAN_TOPIC_ARN', project.BRIAN_TOPIC_ARN)
	systemProperty('aws.accessKeyId', project.awsAccessKey)
	systemProperty('aws.secretKey', project.awsSecretKey)
	
	if (project.hasProperty('springProfilesActive')) {
		systemProperty('spring.profiles.active', project.springProfilesActive)
	}
	if (project.hasProperty('logbackConfigurationFile')) {
		systemProperty('logback.configurationFile', project.logbackConfigurationFile)
	}
}


// ======== packaging ========
war {
	manifest {
		attributes(
			"Implementation-Title": project.name,
			"Implementation-Version": version,
			"Implementation-Timestamp": new Date()
		)
	}
}


// ======== release ========
apply plugin: 'aws'

aws {
	assert project.hasProperty('awsAccessKeyForRelease') && project.hasProperty('awsSecretKeyForRelease'): "AWS credential is required"
	accessKeyId awsAccessKeyForRelease
	secretKey awsSecretKeyForRelease
	region 'ap-northeast-1'
}

apply plugin: 'aws-s3'

task uploadWar(type: jp.classmethod.aws.gradle.s3.AmazonS3FileUploadTask, dependsOn: war) {
	bucketName 'cm-public-eb-applications'
	key "${artifactId}/${artifactId}-${version}.war"
	file project.war.archivePath
	overwrite project.version.endsWith('-SNAPSHOT')
}

task uploadArchives(dependsOn: uploadWar)


// ======== wrapper ========
task wrapper(type: Wrapper) {
	gradleVersion = '2.1'
}

